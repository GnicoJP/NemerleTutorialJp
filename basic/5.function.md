# メソッド・関数
関数の定義の仕方は次の通りです．C#やGCC拡張と同様に関数の中に関数を定義することもできます．
```
<MethodName>(<ArgumentName1> : <ArgumentType1>, <ArgumentName2> : <ArgumentType2>...) : <ReturnType> {
  <Code>
}
```

`MethodName`：メソッド名  
`ArgumentName` : 引数名  
`ArgumentType` : 引数の型名  
`ReturnType` : 返り値の型名  

具体例は以下の通りです．

```csharp
void HogeHogeMethod(int start, int count) {
  //DoSomething
} //C#
```
```fsharp
let hogeHogeMethod (start : int, count : int) : unit =
  //DoSomething
  //F#
  ()
```
```nemerle
HogeHogeMethod(start : int, count : int) : void {
  //DoSomething
} //Nemerle
```

上の3つはほぼ同じです．
書き方はC#をF#みたく書いた感じでしょうか．

## 型引数を持った関数定義
型引数を持った場合は次のようになります．
```
<MethodName>[<TypeArgument1>, <TypeArgument2>...](<ArgumentName1> : <ArgumentType1>, <ArgumentName2> : <ArgumentType2>...) : <ReturnType> {
  <Code>
}
```

`MethodName`：メソッド名  
`TypeArgument` : 型引数
`ArgumentName` : 引数名  
`ArgumentType` : 引数の型名  
`ReturnType` : 返り値の型名  

次のように書きます．  
```nemerle
wellFormat[T](data : T) : string {
  "LOG: $data"
}
```

## 引数の無視
引数名に`_`を指定すれば，その引数を無視します．

```nemerle
Add3(x : int, _ : int, _ : int) : int {
  x * 3;
}
```

## 関数のreturn
C#ならば，

```csharp
int Add(int x, int y) {
  return x + y;
}
```
ですが，Nemerleでは，`return`は要りません．最後の式の戻り値が返されます．

```nemerle
Add(x : int, y : int) : int {
  x + y;
}
```
（Rustと違って，`;`はあってもなくても大丈夫です．）  

また，関数の戻り値を無視するには`_ =`を加えて，無視すること明記しないとワーニングになります．
```nemerle
def ret = Add(3, 8);
Add(4, 2); // Warning
_ = Add(10, 2);
```

## ref/out
C#同様に`ref`や`out`も指定できます．`in`はありません．（10年前はありませんでした．）  
型の前に指定します．  
```nemerle
Increment(x : ref int) : void{
  x++;
}
Parse(x : char, y : out int) : void {
  y = if(x=='0') 0 else 1
}
```

C#と同様に`ref`/`out`を付けた変数を関数内で変更すると呼び出し元の変数も変えることができます．また，`out`は関数を出る前に必ず値を代入する必要があります．  

## オプション引数
C#同様に引数が与えられなかった場合のデフォルトの引数を指定できます．
```nemerle
Show(x : int = 10) {
  Console.WriteLine(x);
}

Show(2); // 出力:2
Show(); // 出力:10
```

# 高階関数
関数型言語の特徴を持つので，関数を第一級オブジェクトとして使うことができます．
型の書き方は次の通りです．

```
<ArgumentType1> * <ArgumentType2> * ... -> <ReturnType>
```

`ArgumentType`：引数の型名  
`ReturnType` : 返り値の型名  

例えば，次の関数  
```nemerle
hoge(fuga : int, piyo : char, nyan : string) : void {
  //...
}
```
の型は  
```nemerle
int * char * string -> void
```
です．
引数無しであれば，`void -> ReturnType`になります．

# ラムダ式(匿名関数)
ラムダ式は次のように書きます．  

```
fun(<ArgumentName1> : <ArgumentType1>, <ArgumentName2> : <ArgumentType2>...) : <ReturnType> {
  <Code>
}
```

あるいは，C#と同様に`=>`を用いることもできます．(`Nemerle.Core.=>`マクロ)  

```
(<ArgumentName1>, <ArgumentName2>...) => {
  <Code>
}
```

`ArgumentName` : 引数名  
`ArgumentType` : 引数の型名  
`ReturnType` : 返り値の型名  

普通の関数定義と違い，引数の型名を省略することができます．（型推論）  

```csharp
Func<int,long> intTolong = (n) => (long)n;
Func<int,int,long> Add = (n, m) => (long)(n + m);
```

```nemerle
mutable intTolong = fun(n : int) : long {n :> long;};
mutable Add : int * int -> long = fun(n, m) {(n + m) :> long;};
//型推論も効きます（分かる場合は）
intTolong = fun(n) {n :> long;};
Add = fun(n, m) {(n + m) :> long;};

//C#ライクな書き方もできます（Nemerle.Core.=>）
intTolong = (n) => n :> long;
```

## 部分適用
F#と違ってカリー化できませんが，部分適用のための構文はあります．`_`を使います．  
例えば，
```nemerle
def increment = _ + 1;
```
は
```nemerle
def increment = fun(x){x + 1}
```
と等価です．
これは演算子,関数だけでなく，
```nemerle
def strToStringBuilder : string -> StringBuilder = StringBuilder(_);
                                                // fun(s) {StringBuilder(s)};
def getLength : string -> int = _.Length;
                             // fun(s) {s.Length};
```
プロパティなどにも使えます．

PREV : [4.演算子](4.operators.md)  
NEXT : [6.名前空間とクラス](6.namespace_class.md)