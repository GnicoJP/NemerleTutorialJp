# namespace : 名前空間の定義
C#/VB.netの`namespace`と同じです．
```
namespace <NamespaceName> {
    //...
}
```

```nemerle
namespace hoge {
  class hoge {
    //...
  }

  module hogeUtil {
    //...
  }
}
```

## using
### namespaceへのusing
`using`を使うと，`namespace`名を省略できます．
```nemerle
System.Console.WriteLine("hogehoge");
```
↓
```nemerle
using System;

Console.WriteLine("hogehoge");
```

### static classへのusing
`static class`名も省略できます．
```nemerle
System.Console.WriteLine("hogehoge");
```
↓
```nemerle
using System.Console;

WriteLine("hogehoge");
```

### リネームのためのusing
`using`を使うと，`namespace`にエイリアスが付与できます．
```nemerle
System.Console.WriteLine("hogehoge");
```
↓
```nemerle
using Con = System.Console;

Con.WriteLine("hogehoge");
```

# class : クラスの定義
C#/Vb.netの`class`と同じで，クラスを定義します．
クラスの定義の仕方は後で詳しく説明します．

```
class <ClassName> {
    //...
}  
```

```nemerle
class hoge {
  mutable a : int;
}

Main() : void {
  def h = hoge();
  h.a = 0;
  WriteLine(h.a);  // 0
}
```

## module
VisualBasic .Netの`module`, C#の`static class`と同じです．  
`module`の中に書いた物は自動的にStaticになります．
通常のクラスのコンストラクタではなく，静的コンストラクタが定義できます．

```
module <ModuleName> {
    //...
}  
```

```nemerle
module Num {
  this() {  // 静的コンストラクタ．クラスと同様の書き方です．

  }
  Add(x : int, y : int) : void {
    x + y
  }
}

Main() : void {
  Console.WriteLine(Num.Add(3, 5)); // 8
  def n : Num = Num(); // Error!!
}
```

## クラスの定義
Nemerleでも，もちろん.Netのクラスを定義することができます．他の言語と同じで，プロパティなども定義することができます．そこまで，C#/VB .netと差異はありません．

```nemerle
class IntPair {
  mutable a : int; // メンバ変数(アクセス修飾子がないので，自動的にprivateになる)
  public mutable b : int; // アクセス修飾子がつけられる
  private c : int; // ミュータブルが無ければ局所定数(変更はできない)
  static class_name : string = "IntPair";
    //staticをつければ静的に，”=”で代入もできる

  public A : int { // プロパティ．この書き方はC#と同じなので，そこまで気にしなくていい．
    get{
      WriteLine("A is called!");
      a;
    } set{
      a = value;
    }
  }
  
  public C : int {get; set;}

  public this() { //コンストラクタ
    a = 0;
    b = 0:
    c = 0;
  }

  public static this() { //静的なコンストラクタ
    WriteLine("IntPair!");
  }
}
```
プロパティをもっと短く書くには[Accessorマクロ](../macro/accessor_flagaccessor.md)を使用します．

## 総称型と型パラメータ
型パラメータは関数と同様にクラス名の後に`[]`で付けます．

## インデクサ
VisualBasic寄りな書き方で書くことができます．

```nemerle
class Table[T] {
  store : array[array[T]];
  public Item[row : int, column : int] : T
  {
    get {
      System.Console.WriteLine ($"get ($row, $column)");
      store[row][column]
    }
    set {
      System.Console.WriteLine ($"set ($row, $column)");
      store[row][column] = value
    }
  }
  public this()
  {
    store = array (10);
    for (mutable i = 0; i < store.Length; ++i)
      store[i] = array (10);
  }
}

def t = Table[string]();
t[2, 3] = "foo";
t[2, 2] = t[2, 3] + "bar";

/* Output:
set (2, 3)
get (2, 3)
set (2, 2)
*/
```

## 演算子オーバーロード
C#と違って記号に制限がありません．
staticな関数を宣言し，変数名を`@`で始めます．

```nemerle
class hoge {
  public n : int;
  public this(i : int) {
    n = i;
  }

  public static @<-< (x : hoge, y : hoge) {
    hoge(x.n + y.n);
  }
}

def a = hoge(12);
def b = hoge(22);
a <-< b > WriteLine;
```

## イベント
C#と同様に書くことができます．
```nemerle
class Button {
  public event OnClick : Callback;
}

def b = Button();
def my_cb = fun() { ... };
b.OnClick += my_cb;
b.OnClick -= my_cb;

b.OnClick += fun() { ... };
```

## 継承
継承もC#と同様です．
```nemerle
class Android : Linux {  ... }
```
継承元の関数等を呼び出すには`this`ではなく，`base`を使います．

# Interface : インターフェース
既定の実装は書けません．（2011年は.NETランタイム的に無理でした．）  
C#と同じ書き方です．
```nemerle
interface IPrintable {
  Print() : void;
}

class RefrigeratorNG : Refrigerator, IPrintable
{
  public Print() : void
  {
    printf ("I'm the refrigerator!\n")
  }

  public this()
  {
  }
}

module RP {
  PrintTenTimes(p : IPrintable) : void
  {
    for(mutable i = 0; i < 10; ++i)
      p.Print()
  }

  Main() : void
  {
    def refr = RefrigeratorNG();
    PrintTenTimes(refr)
  }
```

# Record : 構造体
classに[Record]マクロをつけると，C#/VB .netの構造体のようになります．

```nemerle
[Record] class Point {
  public mutable x : double = 0;
  public mutable y : double = 0;
}
```

# enum : 列挙型
`enum`もあります．
```nemerle
enum Color {
  | Red
  | Green
  | Blue
}

def string_of_color(e) {
  match(e) {
    | Color.Red => "red"
    | Color.Green => "green"
    | Color.Blue => "blue"
  }
}
```
ビットフィールドも出来ます．
```nemerle
using System;

[FlagsAttribute]
enum Seasons {
  | Winter = 0b0001
  | Spring = 0b0010
  | Summer = 0b0100
  | Fall = 0b1000
  | Autumn = Fall
  | AllSeasons = Winter + Spring + Summer + Fall
}

def seasons = Seasons.Winter | Seasons.Fall;
Console.WriteLine ($"Seasons: $(seasons.ToString())");  //Seasons: Winter, Fall
```


# 修飾子
ほとんど[C#と同じです.](https://msdn.microsoft.com/ja-jp/library/ms173121.aspx)
## アクセス修飾子
* `public`：どこからでも参照できます．
* `private`：クラス内でしか参照できません．
* `protected`：そのクラスと継承したクラスでしか参照できません．
* `internal`：アセンブリの内部でしか参照できません．
* `protected internal`：アセンブリの内部にあるクラスと継承したクラスでしか参照できません．

## 継承
* `abstract` : 実装を省略します．（`abstract class`内でのみ`abstract`な関数が作れる．）
* `virtual` : 継承先のクラスでの実装を上書き出来る関数を示します．
* `override` : 継承先のクラスでの実装を上書きします．`virtual`が付いたクラスのみ`override`可能です．
* `sealed` : `new`不可である関数を示します．
* `new` : 継承先のクラスで関数を再定義します．

## そのほか
* `static`：静的です．ClassName.StaticMemberの形で参照できるようになります．
* `mutable`：値が可変であることを示します．
* `volatile`：その値がキャッシュの影響を受けないことを保証します．
* `extern`：P/Invokeで使用します．
* `partial`：`class`や`module`を分割して定義できるようにします．

PREV : [5.関数](5.function.md)  
